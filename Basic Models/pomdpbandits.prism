pomdp

observables
//v1 and v2 are when the scheduler picks bandit1 or 2 it can observe them.
		s, turn, k, v1, v2
endobservables

module bandit1

		// bandit is either on or off
		b1 : [0..1];
		//at first 50-50 chance it will be on or off
		[initial] true -> 0.5 : (b1'=0) + 0.5 : (b1'=1);

		//action 1
		[a1] b1=0 -> 0.3:(b1'=1) + 0.7:(b1'=0);
		[a1] b1=1 -> 0.4:(b1'=0) + 0.6:(b1'=1);

endmodule

module bandit2

		b2 : [0..1];

		[initial] true -> 0.5 : (b2'=0) + 0.5 : (b2'=1);

		[a1] b2=0 -> 0.3:(b2'=1) + 0.7:(b2'=0);
		[a1] b2=1 -> 0.4:(b2'=0) + 0.6:(b2'=1);
endmodule

module scheduler

		s : [0..2];
		turn : [-1..1];
		v1 : [0..1];
		v2 : [0..1];

		//when turn goes from -1 to 0 initialize bandits.
		[initial] turn=-1 -> (turn'=0);
		//turn 0 to 1 will rotate both bandits
		[a1] turn=0 -> (turn'=1);
		//turn 1 to 0 will pick any bandit
		[choose1] turn=1 -> (s'=1)&(turn'=0)&(v1'=b1);
		[choose2] turn=1 -> (s'=2)&(turn'=0)&(v2'=b2);

endmodule

const int kmax; // bound on the steps

module counter 

	 k : [0..kmax+1];
	[a1] k<kmax -> true;
	[choose1] k<kmax -> (k'=min(kmax,k+1));
	[choose2] k<kmax -> (k'=min(kmax,k+1));
	[] k=kmax -> (k'=k+1);

endmodule

label "correct1" = s=1 & b1=1;
label "correct2" = s=2 & b2=1;

rewards "correct_guess"
	turn=0 & s=1 & b1=1 : 1;
	turn=0 & s=2 & b2=1 : 1;
endrewards
